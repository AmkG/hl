; reader

(in-package impl)

; axioms
(import <axiom>lambda fn)
(import <axiom>set =)
(import <axiom>is is)
(import <axiom>rep rep)
(import <axiom>car car)
(import <axiom>cdr cdr)
(import <axiom>cons cons)
(import <axiom>table-sref table-sref)
(import <axiom>table-ref table-ref)
(import <axiom>i-to-c i-to-c)
(import <axiom>c-to-i c-to-i)
(import <axiom>sy-to-s sy-to-s)
(import <axiom>s-to-sy s-to-sy)
(import <axiom>string-build string-build)
(import <axiom>string-length string-length)
(import <axiom>string-ref string-ref)
(import <axiom>string-sref string-sref)
(import <axiom>f< f<)
(import <axiom>i+ +)
(import <axiom>i- -)
(import <axiom>i* *)
(import <axiom>i< <)

; functions
(import <hl>no no)
(import <hl>table table)
(import <hl>err err)
(import <hl>string string)
(import <hl>uniq uniq)
(import <hl>readc readc)
(import <hl>peekc peekc)
(import <hl>unreadc unreadc)
(import <hl>>= >=)
(import <hl><= <=)
(import <hl>listtab listtab)
(import <hl>coerce coerce)
(import <hl>rev rev)
(import <hl>unpkg unpkg)

; macros
(import <hl>mac mac)
(import <hl>def def)
(import <hl>if if)
(import <hl>let let)
(import <hl>with with)
(import <hl>withs withs)
(import <hl>afn afn)
(import <hl>self self)
(import <hl>and and)
(import <hl>or or)
(import <hl>when when)
(import <hl>unless unless)
(import <hl>each each)
(import <hl>while while)


; symbol syntax table. Maps chars to a pair holding function to be applied
; and symbol precedence. The function accepts two params: the entire symbol
; and the position where the matching char is. The symbol precedence is a
; positive floating point number.
(def (mk-ssyntax-table) (table))

; add ssyntax to current context
(def (add-ssyntax c prec f)
  (let stb (table-ref (rep (current-ctx)) 'ssyntax-table)
    (table-sref stb c (cons f prec)))) 

; expand symbol s, if it has symbol syntax
; it's up to the registerd ssyntax functions to recursively call
; ssyntax-expand on generated symbols
(def (ssyntax-expand s)
  (withs (stb (table-ref (rep (current-ctx)) 'ssyntax-table)
          rep (sy-to-s s)
          ln (string-length rep))
    (let winner ((afn (pos winner max-prec)
                   ; find the symbol syntax character with the highest
                   ; precedence
                   (if (is pos ln)
                     winner
                     (let p (table-ref stb (string-ref rep pos))
                       (if (and p (f< max-prec (cdr p)))
                         (self (+ pos 1) (cons (car p) pos) (cdr p))
                         (self (+ pos 1) winner max-prec)))))
                  0 nil (<axiom>i-to-f -1))
      (if winner
        ((car winner) s (cdr winner))
        s))))

; split a symbol in two parts at position pos
; character at position pos is left out
; !! converting back and forth between symbols
; !! and strings is *not* cheap.  Better to stick
; !! with strings until you're sure the string is
; !! just a completely plain symbol, then convert
; !! once.
; !! or not, since reading doesn't need to be
; !! fast, but I suspect it's much easier to
; !! handle strings.
(def (split-sym s pos)
  (withs (s (sy-to-s s)
          ln (string-length s)
          left (string-build pos)
          right (string-build (- ln (+ pos 1))))
    ((afn (i)
       (when (< i ln)
         (if (< i pos) (string-sref left i (string-ref s i))
             (< pos i) (string-sref right (- i (+ pos 1)) (string-ref s i)))
         (self (+ i 1)))) 0)
    (cons (s-to-sy left) (s-to-sy right))))

; create a ssyntax function that splits the symbol in two and calls f
; passing the two ssyntax-expanded sides
(def (mk-binary-ssyntax f)
  (fn (s pos)
    (if (or (is pos 0) (is pos (string-length (sy-to-s s))))
      (err:string "binary syntax must be in the middle of a symbol: " s)
      (let sides (split-sym s pos)
        (f (ssyntax-expand (car sides)) (ssyntax-expand (cdr sides)))))))

; convenient wrapper around mk-binary-ssyntax
     ; !! note that we cannot, in fact, use this macro here.
     ; !! for various reasons, this macro is inaccessible to
     ; !! the compiler, which is hosted on another system.
(mac (def-binary-ssyntax name args . body)
  (list '= name (list 'mk-binary-ssyntax 
                      (cons 'fn (cons (list (car args) (cadr args)) body)))))

; get current context
(def (current-ctx)
  ; current context is stored in a process local variable
  ; !! a process might want to maintain multiple
  ; !! independent contexts.
  ; !! how about *passing in* the current context instead?
  ; !! we also need to fix the accepted shortcut for
  ; !! "context": arc-f uses "cxt", much of the code
  ; !! here uses "ctx".
  (table-ref (<common>proc-locals) 'current-ctx))

; get the current read table
(def (ctx-read-table)
  (table-ref (rep (current-ctx)) 'read-table))

; many reader functions stop on some special characters
; it's up to these functions to stop, i.e. it isn't compulsory
; like whitespace, parenthesis, etc.
; this special list is part of the current context

; add a "blocking" character to the current list
(def (ctx-block-on c)
  ; use a table for faster lookups
  (table-sref (table-ref (rep (current-ctx)) 'block-on) c t))
; add a "separator" character to the current list
(def (ctx-add-sep c)
  (table-sref (table-ref (rep (current-ctx)) 'sep) c t))

; tells if c is a blocking character in the current context
(def (should-block c)
  (or (is-sep c)
      (table-ref (table-ref (rep (current-ctx)) 'block-on) c)))
; tells if c is a separator character
(def (is-sep c)
  (table-ref (table-ref (rep (current-ctx)) 'sep) c))

; get the current context and initialise it with
; default read table
(def (init-default-read-ctx ctx)
  (let ctx (rep ctx)
    (table-sref ctx 'block-on (table))
    (table-sref ctx 'sep (table))
    (each c '(#\space #\return #\newline #\tab)
      (ctx-add-sep c))
    (each c '(#\; #\( #\) #\[ #\] #\' #\, #\`)
      (ctx-block-on c))
    (table-sref ctx 'ssyntax-table (mk-ssyntax-table))
    (let read-table (table)
      ; populate read table
      (each c '(#\space #\return #\newline #\tab)
        (table-sref read-table c ignore))
      (table-sref read-table #\; comment)
      (each c '(#\+ #\- #\0 #\1 #\2 #\3 #\4 #\5 #\6 #\7 #\8 #\9)
        (table-sref read-table c read-num))
      (table-sref read-table #\# read-#)
      (table-sref read-table #\( read-list)
      (table-sref read-table #\[ read-fn-abbr)
      (table-sref read-table #\` read-qq)
      (table-sref read-table #\, read-unq)
      (table-sref read-table #\' read-quote)
      ; default entry
      (table-sref read-table t read-sym)
      (table-sref ctx 'read-table read-table))))

; !! another idea:
; !! each read function returns either a nil,
; !! or a cons cell containing the read object
; !! the cons cell can be reused by the list-reading
; !! read function (handler for left-paren), and in
; !! the general case, an object will be read in as
; !! part of a list anyway.
; !! uniq symbols in hl might not be cheap: creating
; !! one may require at least 2 interprocess messages.
; !! throwing away cons cells might be cheaper!
; The read table maps character to read functions
; each read function accepts the matched character,
; the input stream and an object meaning "nothing" (a uniq symbol)
; it returns the read object or the special object
; "nothing"
; the read table must have at least one entry mapping the object 't
; to a special catch-all function, called when there is no other
; matching function

; find reader function for character c
(def (get-read-fn c)
  (let read-table (ctx-read-table)
    (or (table-ref read-table c)
        (table-ref read-table t))))

; main reader loop
(def (base-read in eof)
  (let c (readc in)
    (if (is c nil) ; eof
      eof
      (withs (read-fn (get-read-fn c)
              nothing (uniq)
              res (read-fn c in nothing))
        (if (is res nothing)
          ; nothing read, keep going
          (base-read in eof)
          res)))))

; a simple read table entry: ignore the character
(def (ignore c in nothing)
  nothing)

; skip a comment up to end of line
(def (comment c in nothing)
  (if (is (peekc in) #\newline)
    nothing ; done
    (do 
      (readc in) ; throw away
      (comment c in nothing))))

; tells if c is a digit
(def (adigit c)
  (and (>= (c-to-i c) (c-to-i #\0)) (<= (c-to-i c) (c-to-i #\9))))

; read a number (integer or floating point)
; FIXME: add support for inf, nan, floats with exponent (e.g. 1.2e10)
(def (read-num c in nothing)
  (with (sign (if (is c #\-) -1 1)
         n (if (or (is c #\-) (is c #\+)) 0 (- (c-to-i c) (c-to-i #\0))))
    (= c (readc in)) ; next
    (while (adigit c)
      (= n (+ (* n 10) (- (c-to-i c) (c-to-i #\0))))
      (= c (readc in)))
    (if (is c #\.) ; floating point
      (let p (<axiom>i-to-f 10)
        (= c (readc in))
        (while (adigit c)
          (= n (+ n (/ (- (c-to-i c) (c-to-i #\0)) p)))
          (= p (* p 10))
          (= c (readc in)))))
    (unreadc in c) ; last character read isn't part of the number
    ; FIXME: if last character read isn't blocking, read up to a
    ; blocking char and return a symbol 
    (* sign n)))



(let escape-table (listtab '((#\" #\") (#\\ #\\) (#\n #\newline) (#\t #\tab)))
  (def (escape-char c)
    (let res (table-ref escape-table c)
      (if res
        res 
        (err:string "Don't know how to escape char " c)))))

; FIXME: add support for unicode chars in the form \uNNNN
(def (read-string c in nothing)
  (with (escape-next nil
         res nil)
    (while (do (= c (readc in))
               (no (and (is c #\") (no escape-next))))
      (unless c (err "eof found while reading string"))
      (if 
        escape-next (do 
                      (= escape-next nil) 
                      (= res (cons (escape-char c) res)))
       (is c #\\) (= escape-next t)
       (no (is c #\")) (= res (cons c res))))
    (coerce (rev res) 'string)))

(def (read-sym c in nothing)
  (let res nil
    (while (do (= c (readc in)) (and c (no (should-block c))))
      (= res (cons c res)))
    (ssyntax-expand (s-to-sy (coerce (rev res) 'string)))))

(def (read-# c in nothing)
  (let next (readc in)
    ; TODO: change if with table based approach
    (if
      (is next #\\) (read-char next in nothing)
      (do
        ; default action: read as a symbol
        (unreadc in next)
        (read-sym c in nothing)))))

(let name->char (table (<axiom>s-to-sy "newline")  #\newline
                       (<axiom>s-to-sy "tab")      #\tab
                       (<axiom>s-to-sy "null")     #\null
                       (<axiom>s-to-sy "space")    #\space
                       (<axiom>s-to-sy "\\")       #\\ ) ; map char names to actual chars
  (def (read-char c in nothing)
    (= c (readc in))
    (if (should-block (peekc in))
      c ; straight char
      (do
        ; TODO: add check for unicode chars (like #\u12AF)
        ; named char, read the name as a symbol
        (withs (word (read-sym c in nothing)
                res (table-ref name->char word))
          (unless res
            (err:string "Unknown char name: " word))
          res)))))

; throw away separator chars
(def (skip-separators in)
  (let c (readc in)
    (if (is-sep c)
      (skip-separators in)
      (unreadc in c))))

; read a list until character ter is found
(def (read-list-with-ter in ter)
  (skip-separators in)
  (let next (readc in)
    (if (is next ter) 
      nil
      (do 
        (unreadc in next)
        (with (the-car (read in)
               the-cdr nil
               c nil)
          (skip-separators s)
          (= c (readc in))
          (if (is c #\.)
            (do
              ; dotted list
              (= the-cdr (read in)) ; read an object
              ; make sure there are no more objects in the list
              (skip-separators in)
              (unless (is (readc in) ter)
                (err "More than one object follows ."))
              (cons the-car the-cdr))
            (do
              (unreadc in c)
              ; TODO: make tail recursive
              (cons the-car (read-list-with-ter in ter)))))))))

; simple list
(def (read-list c in nothing)
  (read-list-with-ter in #\)))

; function abbreviation: [... _ ...]
(def (read-fn-abbr c in nothing)
  (list '<>fn '(<>_) (read-list-with-ter in #\])))

; quasiquote
(def (read-qq c in nothing)
  (list '<>quasiquote (read in)))

; unquote
(def (read-unq c in nothing)
  (list '<>unquote (read in)))

; quote
(def (read-quote c in nothing)
  (list '<>quote (read in)))

; ~a
(def (ssyntax-complement s pos)
  (if (is pos 0)
          ; !! make sure that built-in ssyntax expansions are *not*
          ; !! packaged, because an interface might redefine them.
          ; !! this also means that ssyntax expansion must
          ; !! occur *before* package contexting.
    (list (unpkg 'complement) (ssyntax-expand (cdr (split-sym s 0))))
    (err:string "~ found in the middle of the symbol: " s)))

; a:b
(def-binary-ssyntax ssyntax-compose (l r)
  (list (unpkg 'm-compose) l r))

; a.b
(def-binary-ssyntax ssyntax-call-one-arg (l r)
  (list l r))

; a!b
(def-binary-ssyntax ssyntax-call-quoted (l r)
  (list l (list (unpkg 'quote) r)))
