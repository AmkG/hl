; structs.arc
; by sacado
; comments by AmkG
; Contains definitions for the abstract syntax trees used
; in CPS- and closure-conversions.

(in-package compiler)
(using <hl>v0)

; a variable
(def make-var (id uid)
  (listtab `((type var) (id ,id) (uid ,uid))))

(def avar (x)
  (and (isa x 'table) (is x!type 'var)))

; a special form (not really a macro, at least not
; an Arc-style macro, since the output of the macro
; should be an AST, not a list)
(def make-sf (id expander)
  (listtab `((type sf) (id ,id) (expander ,expander))))

(def asf (x)
  (and (isa x 'table) (is x!type 'sf)))

;----------------------------------------AST's

; a literal: number or t/nil
; (might be extended to chars someday)
(def make-lit (subx val)
  (listtab `((type lit) (subx ,subx) (val ,val))))

(def alit (x)
  (and (isa x 'table) (is x!type 'lit)))

; a variable reference
(def make-ref (subx var)
  (listtab `((type ref) (subx ,subx) (var ,var))))

(def aref (x)
  (and (isa x 'table) (is x!type 'ref)))

; a quoted form
; (currently only literals)
(def make-quote (subx val)
  (listtab `((type quote) (subx ,subx) (val ,val))))

(def aquote (x)
  (and (isa x 'table) (is x!type 'quote)))

; a (set var val) form
(def make-set (subx var)
  (listtab `((type set) (subx ,subx) (var ,var))))

(def aset (x)
  (and (isa x 'table) (is x!type 'set)))

; an if form
; (supports only 2 or 3-arg form but code
; is preprocessed to that form anyway)
(def make-cnd (subx)
  (listtab `((type cnd) (subx ,subx))))

(def acnd (x)
  (and (isa x 'table) (is x!type 'cnd)))

; a primitive
; (possibly make these *optimized* primitives,
; since it's technically possible, in Arc, to
; redefine most primitives; basically if a
; primitive is redefined, it's no longer
; represented using this)
(def make-prim (subx op)
  (listtab `((type prim) (subx ,subx) (op ,op))))

(def aprim (x)
  (and (isa x 'table) (is x!type 'prim)))

; list of known bytecodes accessible directly to the user
; each bytecode is associated with the number of args it expects
; '* means variadic
; TODO: add a minimum to variadic
(set axioms* 
  (listtab '((<axiom>acquire 0) (<axiom>car 1) (<axiom>scar 2) (<axiom>cdr 1)
             (<axiom>scdr 2) (<axiom>char 1) (<axiom>composeo 0) 
             (<axiom>cons 2) (<axiom>enclose *) (<axiom>disclose 1)
             (<axiom>assemble 1) (<axiom>f-to-i 1) (<axiom>halt 0)
             (<axiom>i-to-f 1) (<axiom>is 2) (<axiom>ccc 1) 
             (<axiom>monomethod *) (<axiom>reducto 0) (<axiom>recv 0)
             (<axiom>release 0) (<axiom>rep 1) (<axiom>self-pid 0)
             (<axiom>send 2) (<axiom>spawn 1) (<axiom>string-create *)
             (<axiom>string-length 1) (<axiom>string-ref 2) 
             (<axiom>string-sref 3) (<axiom>sym 1) (<axiom>symeval 1) 
             (<axiom>table-create 0) (<axiom>table-ref 2) (<axiom>table-sref 3)
             (<axiom>table-keys 1) (<axiom>tag 2) (<axiom>try-recv 0) 
             (<axiom>type 1) (<axiom>do-executor 1) (<axiom>i+ 2) (<axiom>i- 2)
             (<axiom>i* 2) (<axiom>i/ 2) (<axiom>imod 2) (<axiom>i< 2) 
             (<axiom>f+ 2) (<axiom>f- 2) (<axiom>f* 2) (<axiom>f/ 2) 
             (<axiom>f< 2) (<axiom>l-to-b 1) (<axiom>b-ref 2)
             ; I/O
             (<axiom>i/o-accept 2) (<axiom>i/o-appendfile 2) 
             (<axiom>i/o-close 1) (<axiom>i/o-connect 3) (<axiom>i/o-fsync 2)
             (<axiom>i/o-infile 2) (<axiom>i/o-listener 2) 
             (<axiom>i/o-outfile 2)	(<axiom>i/o-pipe 0) (<axiom>i/o-read 3)
             (<axiom>i/o-seek 2) (<axiom>i/o-stderr 0) (<axiom>i/o-stdin 0)
             (<axiom>i/o-stdout 0) (<axiom>i/o-tell 1) (<axiom>i/o-write 3))))

(def check-prim-num-args (op n)
  (let expect (axioms* op)
    (if (and expect (no (is expect '*)) (no (is n expect)))
      (err (string "Not enough arguments given to primitive " op ": " 
                   n " given, " expect " expected.")))))

; check if x is an axiom
(def an-axiom (x)
  (and (isa x 'sym) (axioms* x)))

; a function call (a function *app*lication)
(def make-app (subx)
  (listtab `((type app) (subx ,subx))))

(def anapp (x)
  (and (isa x 'table) (is x!type 'app)))

; a function form (a *lam*bda)
(def make-lam (subx params)
  (listtab `((type lam) (subx ,subx) (params ,params))))

(def alam (x)
  (and (isa x 'table) (is x!type 'lam)))

; a do form
(def make-seq (subx)
  (listtab `((type seq) (subx ,subx))))

(def aseq (x)
  (and (isa x 'table) (is x!type 'seq)))


(def extend (bindings env)
  (+ bindings env))

(def lookup (id env)
  (if (no env)
    nil
    (let head (car env)
      (if (is head!id id)
        head
        (lookup id (cdr env))))))

(= seq-num* 0)

(def new-var (id)
  (++ seq-num*)
  (make-var id (sym (string id "@" seq-num*))))

(def new-global (id)
  (make-var id id))

(def aglobal (var)
  (is var!id var!uid))

